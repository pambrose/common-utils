plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id "com.github.ben-manes.versions" version "0.27.0"
    id "maven-publish"
}

def jvmVersion = '1.8'
def guavaVersion = '28.1-android'
def loggingVersion = '1.7.6'
def kluentVersion = '1.56'
def junitVersion = '5.5.2'

group 'com.sudothought'
description = 'common-utils'
version '1.0.4'
sourceCompatibility = jvmVersion

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "io.github.microutils:kotlin-logging:${loggingVersion}"

    testImplementation "org.amshove.kluent:kluent:$kluentVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

compileKotlin {
    kotlinOptions.jvmTarget = jvmVersion
    kotlinOptions.freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime']
}

compileTestKotlin {
    kotlinOptions.jvmTarget = jvmVersion
    kotlinOptions.freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime']
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pambrose/common-utils")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
    }
}
