plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.0' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.6.21' apply false
    id 'org.jmailen.kotlinter' version "3.10.0" apply false
    id "com.github.ben-manes.versions" version "0.42.0" apply false
}

ext {
    libraries = [
            reflect               : "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",

            coroutines_core       : "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version",

            guava                 : "com.google.guava:guava:$guava_version",

            grpc_netty            : "io.grpc:grpc-netty:$grpc_version",
            grpc_protobuf         : "io.grpc:grpc-protobuf:$grpc_version",
            grpc_stub             : "io.grpc:grpc-stub:$grpc_version",
            grpc_services         : "io.grpc:grpc-services:$grpc_version",

            netty_ssl             : "io.netty:netty-tcnative-boringssl-static:$nettyTcNative_version",

            dropwizard_core       : "io.dropwizard.metrics:metrics-core:$dropwizard_version",
            dropwizard_healthcheck: "io.dropwizard.metrics:metrics-healthchecks:$dropwizard_version",
            dropwizard_servlets   : "io.dropwizard.metrics:metrics-servlets:$dropwizard_version",
            dropwizard_jmx        : "io.dropwizard.metrics:metrics-jmx:$dropwizard_version",

            exposed_core          : "org.jetbrains.exposed:exposed-core:$exposed_version",
            exposed_jdbc          : "org.jetbrains.exposed:exposed-jdbc:$exposed_version",
            exposed_jodatime      : "org.jetbrains.exposed:exposed-jodatime:$exposed_version",

            kotlin_scripting      : "org.jetbrains.kotlin:kotlin-scripting-jsr223:$kotlin_version",

            python_scripting      : "org.python:jython-standalone:$python_version",
            java_scripting        : "ch.obermuhlner:java-scriptengine:$java_version",

            jetty_server          : "org.eclipse.jetty:jetty-server:$jetty_version",
            jetty_servlet         : "org.eclipse.jetty:jetty-servlet:$jetty_version",

            ktor_server_core      : "io.ktor:ktor-server:$ktor_version",
            //ktor_server_plugins  : "io.ktor:ktor-server-plugins:$ktor_version",

            ktor_client_core      : "io.ktor:ktor-client:$ktor_version",
            //ktor_client_cio       : "io.ktor:ktor-client-cio-jvm:$ktor_version",
            //ktor_client_plugins   : "io.ktor:ktor-client-plugins:$ktor_version",

            prometheus_core       : "io.prometheus:simpleclient:$prometheus_version",
            prometheus_hotspot    : "io.prometheus:simpleclient_hotspot:$prometheus_version",
            prometheus_servlet    : "io.prometheus:simpleclient_servlet:$prometheus_version",
            prometheus_dropwizard : "io.prometheus:simpleclient_dropwizard:$prometheus_version",

            redis                 : "redis.clients:jedis:$redis_version",

            zipkin                : "io.zipkin.brave:brave:$zipkin_version",

            zipkin_sender_okhttp  : "io.zipkin.reporter2:zipkin-sender-okhttp3:$zipkinSender_version",
    ]
}

allprojects {
    group 'com.github.pambrose.common-utils'
    version '1.25.0'

    apply plugin: 'java-library'  // gradle 7
    apply plugin: 'maven-publish' // required for jitpack.io install  // gradle 7
    apply plugin: 'com.github.kt3k.coveralls'
    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'com.github.ben-manes.versions'

    repositories {
        mavenLocal()
        maven { url "https://maven.pkg.jetbrains.space/public/p/ktor/eap" }
        google()
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    //cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs

    // gradle 7
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
//                afterEvaluate {
//                    artifact javadocJar
//                    artifact sourcesJar
//                }
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }
}

subprojects {

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        // This is required for the ExperimentalCoroutinesApi arg below
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:$coroutines_version"

        implementation "io.github.microutils:kotlin-logging:$logging_version"
        implementation "ch.qos.logback:logback-classic:$logback_version"

        testImplementation "org.amshove.kluent:kluent:$kluent_version"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    }

    // This is to fix a bizarre gradle error
    project.getTasks().getByName("jar").setProperty("duplicatesStrategy", DuplicatesStrategy.INCLUDE);

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        //archives javadocJar
    }

    java {
        withSourcesJar()
    }

//    task sourcesJar(type: Jar, dependsOn: classes) {
//        from sourceSets.main.allJava
//        archiveClassifier.set("sources")
//    }
//
//    task javadocJar(type: Jar, dependsOn: javadoc) {
//        archiveClassifier.set("javadoc")
//        from javadoc.destinationDir
//    }

    compileKotlin {
        kotlinOptions {
            //useIR = true
            jvmTarget = '1.8'
            freeCompilerArgs += ['-Xbackend-threads=8',
                                 '-opt-in=kotlin.contracts.ExperimentalContracts',
                                 '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                                 '-opt-in=kotlin.time.ExperimentalTime']
        }
    }

    compileTestKotlin {
        kotlinOptions {
            //useIR = true
            jvmTarget = '1.8'
            freeCompilerArgs += ['-Xbackend-threads=8',
                                 '-opt-in=kotlin.time.ExperimentalTime']
        }
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
            showStandardStreams = true
        }
    }

    kotlinter {
        ignoreFailures = false
        // indentSize = 2
        reporters = ['checkstyle', 'plain']
        experimentalRules = false
        disabledRules = ["no-wildcard-imports", "indent", "final-newline", "comment-spacing", "max-line-length", "wrapping"]
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    coveralls {
        jacocoReportPath 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
    }
}